# @file stdlib/queue/pop.swl
# Remove a element from the queue.
#
#  Copyright (C) 2008-2012  Xos√© Otero <xoseotero@gmail.com>
#
#  This program is free software: you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published by
#  the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#
#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU General Public License
#  along with this program.  If not, see <http://www.gnu.org/licenses/>.


.include "stdlib/def.swl"
.include "stdlib/error.swl"
.include "stdlib/address/address.swl"
.include "stdlib/node/def.swl"
.include "stdlib/node/remove.swl"
.include "stdlib/queue/def.swl"


# Remove a element from the queue.
# IN:  g0 pointer to the queue
# OUT: g0 element removed
# OUT: g1 error status
#      STD_NOERROR: no errors
#      STD_EINVAL: the queue is empty
#      STD_EFAULT: the pointer is not valid
.label std_queuepop
        move r0 g0                      # pointer

        # check if the pointer is valid
        loadi g1 STD_QUEUE_STRUCT
        call std_address
        bz g0 _std_queuepop_efault

        # check if the queue is empty
        loadri g0 r0 _STD_QUEUE_BOTTOM
        bz g0 _std_queuepop_einval

        loadri r1 g0 _STD_NODE_NEXT     # newbottom
        loadri r2 g0 STD_NODE_DATA      # data

        # remove the bottom node
        call std_noderemove
        bnz g1 _std_queuepop_efault

        # update bottom
        storeri r0 r1 _STD_QUEUE_BOTTOM

        # check if the queue is empty
        bnz r1 _std_queuepop_ret
        # update top
        storeri r0 r1 _STD_QUEUE_TOP

        .label _std_queuepop_ret
        move g0 r2
        loadi g1 STD_NOERROR
        ret

        .label _std_queuepop_einval
        loadi g1 STD_EINVAL
        ret

        .label _std_queuepop_efault
        loadi g1 STD_EFAULT
        ret
