# @file stdlib/mem.swl
# Operations over the memory.
#
#  Copyright (C) 2008-2010  Xos√© Otero <xoseotero@gmail.com>
#
#  This program is free software: you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published by
#  the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#
#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU General Public License
#  along with this program.  If not, see <http://www.gnu.org/licenses/>.

.include "stdlib/def.swl"
.include "stdlib/error.swl"
.include "stdlib/address.swl"

# Set all the words of a block of memory to a value.
# IN:  g0 pointer
# IN:  g1 size of the block of memory
# IN:  g2 value
# OUT: g1 error status
#      STD_NOERROR: no errors
#      STD_EINVAL:  size is not valid
#      STD_EFAULT:  the pointer is not valid
.label std_fill
        # check if the size is valid
        bz g1 _std_fill_einval
        modi g3 g1 STD_WORDSIZE
        bnz g3 _std_fill_einval

        move r0 g0                      # pointer
        move r1 g1                      # size
        move r2 g2                      # value

        # check if the block is valid
        call std_address
        bz g0 _std_fill_efault

        # set all the block to the value
        .label _std_fill_loop
        subi r1 r1 STD_WORDSIZE
        storerr r0 r2 r1
        bnz r1 _std_fill_loop

        .label _std_fill_ok
        loadi g1 STD_NOERROR
        ret

        .label _std_fill_einval
        loadi g1 STD_EINVAL
        ret

        .label _std_fill_efault
        loadi g1 STD_EFAULT
        ret

# Set all the half words of a block of memory to a value.
# IN:  g0 pointer
# IN:  g1 size of the block of memory
# IN:  g2 value
# OUT: g1 error status
#      STD_NOERROR: no errors
#      STD_EINVAL:  size is not valid
#      STD_EFAULT:  the pointer is not valid
.label std_fillh
        # check if the size is valid
        bz g1 _std_fillh_einval
        modi g3 g1 STD_HWORDSIZE
        bnz g3 _std_fillh_einval

        move r0 g0                      # pointer
        move r1 g1                      # size
        move r2 g2                      # value

        # check if the block is valid
        call std_address
        bz g0 _std_fillh_efault

        # set all the block to the value
        .label _std_fillh_loop
        subi r1 r1 STD_HWORDSIZE
        storehrr r0 r2 r1
        bnz r1 _std_fillh_loop

        .label _std_fillh_ok
        loadi g1 STD_NOERROR
        ret

        .label _std_fillh_einval
        loadi g1 STD_EINVAL
        ret

        .label _std_fillh_efault
        loadi g1 STD_EFAULT
        ret

# Set all the quarter words of a block of memory to a value.
# IN:  g0 pointer
# IN:  g1 size of the block of memory
# IN:  g2 value
# OUT: g1 error status
#      STD_NOERROR: no errors
#      STD_EINVAL:  size is not valid
#      STD_EFAULT:  the pointer is not valid
.label std_fillq
        # check if the size is valid
        bz g1 _std_fillq_einval

        move r0 g0                      # pointer
        move r1 g1                      # size
        move r2 g2                      # value

        # check if the block is valid
        call std_address
        bz g0 _std_fillq_efault

        # set all the block to the value
        .label _std_fillq_loop
        subi r1 r1 STD_QWORDSIZE
        storeqrr r0 r2 r1
        bnz r1 _std_fillq_loop

        .label _std_fillq_ok
        loadi g1 STD_NOERROR
        ret

        .label _std_fillq_einval
        loadi g1 STD_EINVAL
        ret

        .label _std_fillq_efault
        loadi g1 STD_EFAULT
        ret


# Copy a block of memory (quarter word by quarter word).
# IN:  g0 destination pointer
# IN:  g1 source pointer
# IN:  g2 size of the block of memory
# OUT: g1 error status
#      STD_NOERROR: no errors
#      STD_EINVAL:  size is not valid
#      STD_EFAULT:  the pointers are not valid
.label std_copy
        # check if the size is valid
        bz g1 _std_copy_einval

        move r0 g0                      # dst
        move r1 g1                      # src
        move r2 g2                      # size

        # check if the dst block is valid
        move g1 g2
        call std_address
        bz g0 _std_copy_efault

        # check if the src block is valid
        move g0 r1
        move g1 r2
        call std_address
        bz g0 _std_copy_efault

        # copy the block
        .label _std_copy_loop
        subi r2 r2 STD_QWORDSIZE
        loadqrr g0 r1 r2
        storeqrr r0 g0 r2
        bnz r2 _std_copy_loop

        .label _std_copy_ok
        loadi g1 STD_NOERROR
        ret

        .label _std_copy_einval
        loadi g1 STD_EINVAL
        ret

        .label _std_copy_efault
        loadi g1 STD_EFAULT
        ret


# Compare two blocks of memory (quarter word by quarter word).
# IN:  g0 pointeg1
# IN:  g1 pointeg2
# IN:  g2 size of the block of memory
# OUT: g0 difference
#      < 0: pointeg1 < pointeg2
#      = 0: pointeg1 = pointeg2
#      > 0: pointeg1 > pointeg2
# OUT: g1 error status
#      STD_NOERROR: no errors
#      STD_EFAULT:  the pointers are not valid
.label std_cmp
        # check if the size is valid
        bz g2 _std_cmp_einval

        move r0 g0                      # pt1
        move r1 g1                      # pt2
        move r2 g2                      # size

        # check if the first block is valid
        move g1 g2
        call std_address
        bz g0 _std_cmp_efault

        # check if the second block is valid
        move g0 r1
        move g1 r2
        call std_address
        bz g0 _std_cmp_efault

        # compare the block
        loadi r3 0x0                    # i

        .label _std_cmp_loop
        loadqrr g0 r0 r3
        loadqrr g1 r1 r3
        sub r4 g0 g1                    # diff
        bnz r4 _std_cmp_notequal

        addi r3 r3 STD_QWORDSIZE
        bne r2 r3 _std_cmp_loop

        .label _std_cmp_equal
        loadi g0 0x0
        loadi g1 STD_NOERROR
        ret

        .label _std_cmp_notequal
        move g0 r4
        loadi g1 STD_NOERROR
        ret

        .label _std_cmp_einval
        loadi g1 STD_EINVAL
        ret

        .label _std_cmp_efault
        loadi g1 STD_EFAULT
        ret


# Find a word in a block of memory.
# IN:  g0 pointer
# IN:  g1 size of the block of memory
# IN:  g2 value
# OUT: g0 offset of the value
# OUT: g1 error status
#      STD_NOERROR: no errors
#      STD_EINVAL:  size is not valid or the value was not found
#      STD_EFAULT:  the pointer is not valid
.label std_find
        # check if the size is valid
        bz g1 _std_find_einval
        modi g3 g1 STD_WORDSIZE
        bnz g3 _std_find_einval

        move r0 g0                      # pointer
        move r1 g1                      # size
        move r2 g2                      # value

        # check if the block is valid
        call std_address
        bz g0 _std_find_efault

        # find the value
        loadi r3 0x0                    # offset

        .label _std_find_loop
        loadrr g0 r0 r3
        beq r2 g0 _std_find_found

        addi r3 r3 STD_WORDSIZE
        bne r1 r3 _std_find_loop

        .label _std_find_einval
        loadi g1 STD_EINVAL
        ret

        .label _std_find_found
        move g0 r3
        loadi g1 STD_NOERROR
        ret

        .label _std_find_efault
        loadi g1 STD_EFAULT
        ret

# Find a half word in a block of memory.
# IN:  g0 pointer
# IN:  g1 size of the block of memory
# IN:  g2 value
# OUT: g0 offset of the value
# OUT: g1 error status
#      STD_NOERROR: no errors
#      STD_EINVAL:  size is not valid or the value was not found
#      STD_EFAULT:  the pointer is not valid
.label std_findh
        # check if the size is valid
        bz g1 _std_findh_einval
        modi g3 g1 STD_HWORDSIZE
        bnz g3 _std_findh_einval

        move r0 g0                      # pointer
        move r1 g1                      # size
        move r2 g2                      # value

        # check if the block is valid
        call std_address
        bz g0 _std_findh_efault

        # find the value
        loadi r3 0x0                    # offset

        .label _std_findh_loop
        loadhrr g0 r0 r3
        beq r2 g0 _std_findh_found

        addi r3 r3 STD_HWORDSIZE
        bne r1 r3 _std_findh_loop

        .label _std_findh_einval
        loadi g1 STD_EINVAL
        ret

        .label _std_findh_found
        move g0 r3
        loadi g1 STD_NOERROR
        ret

        .label _std_findh_efault
        loadi g1 STD_EFAULT
        ret

# Find a quarter word in a block of memory.
# IN:  g0 pointer
# IN:  g1 size of the block of memory
# IN:  g2 value
# OUT: g0 offset of the value
# OUT: g1 error status
#      STD_NOERROR: no errors
#      STD_EINVAL:  size is not valid or the value was not found
#      STD_EFAULT:  the pointer is not valid
.label std_findq
        # check if the size is valid
        bz g1 _std_findq_einval

        move r0 g0                      # pointer
        move r1 g1                      # size
        move r2 g2                      # value

        # check if the block is valid
        call std_address
        bz g0 _std_findq_efault

        # find the value
        loadi r3 0x0                    # offset

        .label _std_findq_loop
        loadqrr g0 r0 r3
        beq r2 g0 _std_findq_found

        addi r3 r3 STD_QWORDSIZE
        bne r1 r3 _std_findq_loop

        .label _std_findq_einval
        loadi g1 STD_EINVAL
        ret

        .label _std_findq_found
        move g0 r3
        loadi g1 STD_NOERROR
        ret

        .label _std_findq_efault
        loadi g1 STD_EFAULT
        ret


# Count the times that a word is found in a block of memory.
# IN:  g0 pointer
# IN:  g1 size of the block of memory
# IN:  g2 value
# OUT: g0 number of elements found
# OUT: g1 error status
#      STD_NOERROR: no errors
#      STD_EINVAL:  size is not valid
#      STD_EFAULT:  the pointer is not valid
.label std_count
        # check if the size is valid
        bz g1 _std_count_einval
        modi g3 g1 STD_WORDSIZE
        bnz g3 _std_count_einval

        move r0 g0                      # pointer
        move r1 g1                      # size
        move r2 g2                      # value

        # check if the block is valid
        call std_address
        bz g0 _std_count_efault

        # count the values
        loadi r3 0x0                    # offset
        loadi r4 0x0                    # counter

        .label _std_count_loop
        loadrr g0 r0 r3
        bne r2 g0 _std_count_notequal
        # element found
        addi r4 r4 0x1

        .label _std_count_notequal
        addi r3 r3 STD_WORDSIZE
        bne r1 r3 _std_count_loop

        .label _std_count_ok
        move g0 r4
        loadi g1 STD_NOERROR
        ret

        .label _std_count_einval
        loadi g1 STD_EINVAL
        ret

        .label _std_count_efault
        loadi g1 STD_EFAULT
        ret

# Count the times that a word is found in a block of memory.
# IN:  g0 pointer
# IN:  g1 size of the block of memory
# IN:  g2 value
# OUT: g0 number of elements found
# OUT: g1 error status
#      STD_NOERROR: no errors
#      STD_EINVAL:  size is not valid
#      STD_EFAULT:  the pointer is not valid
.label std_counth
        # check if the size is valid
        bz g1 _std_counth_einval
        modi g3 g1 STD_HWORDSIZE
        bnz g3 _std_counth_einval

        move r0 g0                      # pointer
        move r1 g1                      # size
        move r2 g2                      # value

        # check if the block is valid
        call std_address
        bz g0 _std_counth_efault

        # count the values
        loadi r3 0x0                    # offset
        loadi r4 0x0                    # counter

        .label _std_counth_loop
        loadhrr g0 r0 r3
        bne r2 g0 _std_counth_notequal
        # element found
        addi r4 r4 0x1

        .label _std_counth_notequal
        addi r3 r3 STD_HWORDSIZE
        bne r1 r3 _std_counth_loop

        .label _std_counth_ok
        move g0 r4
        loadi g1 STD_NOERROR
        ret

        .label _std_counth_einval
        loadi g1 STD_EINVAL
        ret

        .label _std_counth_efault
        loadi g1 STD_EFAULT
        ret

# Count the times that a word is found in a block of memory.
# IN:  g0 pointer
# IN:  g1 size of the block of memory
# IN:  g2 value
# OUT: g0 number of elements found
# OUT: g1 error status
#      STD_NOERROR: no errors
#      STD_EINVAL:  size is not valid
#      STD_EFAULT:  the pointer is not valid
.label std_countq
        # check if the size is valid
        bz g1 _std_countq_einval

        move r0 g0                      # pointer
        move r1 g1                      # size
        move r2 g2                      # value

        # check if the block is valid
        call std_address
        bz g0 _std_countq_efault

        # count the values
        loadi r3 0x0                    # offset
        loadi r4 0x0                    # counter

        .label _std_countq_loop
        loadqrr g0 r0 r3
        bne r2 g0 _std_countq_notequal
        # element found
        addi r4 r4 0x1

        .label _std_countq_notequal
        addi r3 r3 STD_QWORDSIZE
        bne r1 r3 _std_countq_loop

        .label _std_countq_ok
        move g0 r4
        loadi g1 STD_NOERROR
        ret

        .label _std_countq_einval
        loadi g1 STD_EINVAL
        ret

        .label _std_countq_efault
        loadi g1 STD_EFAULT
        ret
