# @file stdlib/set.swl
# Set data structure.
# std_init must be executed before using any of this functions.
#
#  Copyright (C) 2008-2011  Xos√© Otero <xoseotero@gmail.com>
#
#  This program is free software: you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published by
#  the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#
#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU General Public License
#  along with this program.  If not, see <http://www.gnu.org/licenses/>.


.include "stdlib/def.swl"
.include "stdlib/error.swl"
.include "stdlib/address.swl"
.include "stdlib/list/list.swl"
.include "stdlib/list/free.swl"
.include "stdlib/list/size.swl"
.include "stdlib/list/get.swl"
.include "stdlib/list/insert.swl"
.include "stdlib/list/remove.swl"
.include "stdlib/list/find.swl"


# Create a new set.
# OUT: g0 pointer to the new set
# OUT: g1 error status
#      STD_ENOMEM: there is not memory enough
.label std_set
        call std_list
        ret

# Free the set.
# IN:  g0 pointer to the set.
# OUT: g1 error status
#      STD_NOERROR: no errors
#      STD_EFAULT: the pointer is not valid.
.label std_setfree
        call std_listfree
        ret


# Get the number of elements of the set.
# IN:  g0 pointer to the set
# OUT: g0 size of the set
# OUT: g1 error status
#      STD_NOERROR: no errors
#      STD_EFAULT: the pointer is not valid
.label std_setsize
        call std_listsize
        ret


# Get a element of the set.
# IN:  g0 pointer to the set
# IN:  g1 position
# OUT: g0 element
# OUT: g1 error status
#      STD_NOERROR: no errors
#      STD_EINVAL: the position is out of range
#      STD_EFAULT: the pointer is not valid
.label std_setget
        call std_listget
        ret


# Insert a element in the set.
# IN:  g0 pointer to the set
# IN:  g1 value
# OUT: g1 error status
#      STD_NOERROR: no errors
#      STD_EFAULT: the pointer is not valid
#      STD_ENOMEM: there is not memory enough
.label std_setinsert
        move r0 g0                      # pointer
        move r1 g1                      # value

        # check if the pointer is valid
        loadi g1 _STD_LIST_STRUCT
        call std_address
        bz g0 _std_setinsert_efault

        # check if the value is already in the set
        move g0 r0
        loadi g1 0x0
        move g2 r1
        call std_listfind
        bnz g1 _std_setinsert_insert
        loadi g1 STD_NOERROR
        ret

        # insert the value
        .label _std_setinsert_insert
        move g0 r0
        loadi g1 0x0
        move g2 r1
        call std_listinsert
        ret

        .label _std_setinsert_efault
        loadi g1 STD_EFAULT
        ret

# Remove a element from the set.
# IN:  g0 pointer to the set
# IN:  g1 value
# OUT: g1 error status
#      STD_NOERROR: no errors
#      STD_EINVAL: the value is not in the set
#      STD_EFAULT: the pointer is not valid
.label std_setremove
        move r0 g0                      # pointer
        move r1 g1                      # value

        # check if the pointer is valid
        loadi g1 _STD_LIST_STRUCT
        call std_address
        bz g0 _std_setremove_efault

        # search the value in the set
        move g0 r0
        loadi g1 0x0
        move g2 r1
        call std_listfind
        bz g1 _std_setremove_remove
        ret

        # remove the value
        .label _std_setremove_remove
        move g1 g0
        move g0 r0
        call std_listremove
        ret

        .label _std_setremove_efault
        loadi g1 STD_EFAULT
        ret


# Check if a element is in the set.
# IN:  g0 pointer to the set
# IN:  g1 value
# OUT: g0 boolean value
#      STD_TRUE: the value is in the set
#      STD_FALSE: the value is not in the set
# OUT: g1 error status
#      STD_NOERROR: no errors
#      STD_EFAULT: the pointer is not valid
.label std_setcheck
        # search the value in the set
        move g2 g1
        loadi g1 0x0
        call std_listfind
        bz g1 _std_setcheck_found
        loadi g0 STD_FALSE
        loadi g1 STD_NOERROR
        ret

        .label _std_setcheck_found
        loadi g0 STD_TRUE
        loadi g1 STD_NOERROR
        ret
