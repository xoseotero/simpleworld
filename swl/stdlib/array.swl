# @file stdlib/array.swl
# Array data structure.
# std_init must be executed before using any of this functions.
#
#  Copyright (C) 2008-2010  Xos√© Otero <xoseotero@gmail.com>
#
#  This program is free software: you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published by
#  the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#
#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU General Public License
#  along with this program.  If not, see <http://www.gnu.org/licenses/>.

.include "stdlib/def.swl"
.include "stdlib/error.swl"
.include "stdlib/address.swl"
.include "stdlib/alloc.swl"
.include "stdlib/mem.swl"

# Array struct
.define _STD_ARRAY_STRUCT   0x8         # Size of the struct
.define _STD_ARRAY_SIZE     0x0         # Offset to the size field
.define _STD_ARRAY_MEM      0x4         # Offset to the mem field


# Create a new array.
# IN:  r0 number of elements
# OUT: r0 pointer to the array
# OUT: r1 error status
#      STD_NOERROR: no errors
#      STD_ENOMEM: there is not memory enough
.label std_array
        push r0                         # elements is in fp + 0x0

        # allocate the memory
        multi r0 r0 STD_WORDSIZE
        addi r0 r0 _STD_ARRAY_MEM
        call std_alloc
        bnz r1 _std_array_enomem

        # initialize the fields
        loadri r1 fp 0x0
        storeri r0 r1 _STD_ARRAY_SIZE

        .label _std_array_ret
        loadi r1 STD_NOERROR
        ret

        .label _std_array_enomem
        loadi r1 STD_ENOMEM
        ret

# Free the array.
# IN:  r0 pointer to the array.
# OUT: r1 error status
#      STD_NOERROR: no errors
#      STD_EFAULT: the pointer is not valid.
.label std_arrayfree
        call std_free
        ret


# Get the number of elements of the array.
# IN:  r0 number of elements
# OUT: r0 size of the array
# OUT: r1 error status
#      STD_NOERROR: no errors
#      STD_EFAULT: the pointer is not valid
.label std_arraysize
        push r0                         # pointer is in fp + 0x0

        # check if the pointer is valid
        loadi r1 _STD_ARRAY_STRUCT
        call std_address
        bz r0 _std_arraysize_efault

        # get the size
        loadri r0 fp 0x0
        loadri r0 r0 _STD_ARRAY_SIZE

        .label _std_arraysize_ret
        loadi r1 STD_NOERROR
        ret

        .label _std_arraysize_efault
        loadi r1 STD_EFAULT
        ret

# Resize the array.
# IN:  r0 pointer to the array
# IN:  r1 number of elements
# OUT: r1 error status
#      STD_NOERROR: no errors
#      STD_EFAULT: the pointer is not valid
#      STD_ENOMEM: there is not memory enough
.label std_arrayresize
        push r0                         # pointer is in fp + 0x0
        push r1                         # elements is in fp + 0x4

        multi r1 r1 STD_WORDSIZE
        addi r1 r1 _STD_ARRAY_MEM
        call std_realloc
        bz r1 _std_arrayresize_ok
        ret

        .label _std_arrayresize_ok
        push r0                         # new is in fp + 0x8

        # update the size
        loadri r0 fp 0x0
        loadri r1 fp 0x4
        storeri r0 r1 _STD_ARRAY_SIZE

        .label _std_arrayresize_ret
        loadri r0 fp 0x8
        loadi r1 STD_NOERROR
        ret


# Get a element of the array.
# IN:  r0 pointer to the array
# IN:  r1 position
# OUT: r0 element
# OUT: r1 error status
#      STD_NOERROR: no errors
#      STD_EINVAL: the position is out of range
#      STD_EFAULT: the pointer is not valid
.label std_arrayget
        push r0                         # pointer is in fp + 0x0
        push r1                         # position is in fp + 0x4

        # check if the pointer is valid
        loadi r1 _STD_ARRAY_STRUCT
        call std_address
        bz r0 _std_arrayget_efault

        # check if the position is valid
        loadri r0 fp 0x0
        loadri r0 r0 _STD_ARRAY_SIZE
        loadri r1 fp 0x4
        bgeu r1 r0 _std_arrayget_einval

        # get the element
        loadri r0 fp 0x0
        addi r0 r0 _STD_ARRAY_MEM
        loadri r1 fp 0x4
        multi r1 r1 STD_WORDSIZE
        loadrr r0 r0 r1

        .label _std_arrayget_ret
        loadi r1 STD_NOERROR
        ret

        .label _std_arrayget_einval
        loadi r1 STD_EINVAL
        ret

        .label _std_arrayget_efault
        loadi r1 STD_EFAULT
        ret

# Set the value of a element of the array.
# IN:  r0 pointer to the array
# IN:  r1 position
# IN:  r2 value
# OUT: r0 element
# OUT: r1 error status
#      STD_NOERROR: no errors
#      STD_EINVAL: the position is out of range
#      STD_EFAULT: the pointer is not valid
.label std_arrayset
        push r0                         # pointer is in fp + 0x0
        push r1                         # position is in fp + 0x4
        push r2                         # value is in fp + 0x8

        # check if the pointer is valid
        loadi r1 _STD_ARRAY_STRUCT
        call std_address
        bz r0 _std_arrayset_efault

        # check if the position is valid
        loadri r0 fp 0x0
        loadri r0 r0 _STD_ARRAY_SIZE
        loadri r1 fp 0x4
        bgeu r1 r0 _std_arrayset_einval

        # set the element
        loadri r0 fp 0x0
        addi r0 r0 _STD_ARRAY_MEM
        loadri r1 fp 0x4
        multi r1 r1 STD_WORDSIZE
        loadri r2 fp 0x8
        storerr r0 r2 r1

        .label _std_arrayset_ret
        loadi r1 STD_NOERROR
        ret

        .label _std_arrayset_einval
        loadi r1 STD_EINVAL
        ret

        .label _std_arrayset_efault
        loadi r1 STD_EFAULT
        ret


# Set the value of all the elements to a value.
# IN:  r0 pointer to the array
# IN:  r1 value
# OUT: r1 error status
#      STD_NOERROR: no errors
#      STD_EFAULT:  the pointer is not valid
.label std_arrayfill
        push r0                         # pointer is in fp + 0x0
        push r1                         # value is in fp + 0x4

        # check if the pointer is valid
        loadi r1 _STD_ARRAY_STRUCT
        call std_address
        bz r0 _std_arrayfill_efault

        # set the elements
        loadri r0 fp 0x0
        addi r0 r0 _STD_ARRAY_MEM
        loadri r1 fp 0x0
        loadri r1 r1 _STD_ARRAY_SIZE
        multi r1 r1 STD_WORDSIZE
        loadri r2 fp 0x4
        call std_fill
        bz r1 _std_arrayfill_ret
        ret

        .label _std_arrayfill_ret
        loadi r1 STD_NOERROR
        ret

        .label _std_arrayfill_efault
        loadi r1 STD_EFAULT
        ret


# Find a value in a array.
# IN:  r0 pointer to the array
# IN:  r1 start position
# IN:  r2 value
# OUT: r0 poisition of the value
# OUT: r1 error status
#      STD_NOERROR: no errors
#      STD_EINVAL: the value was not found
#      STD_EFAULT: the pointer is not valid
.label std_arrayfind
        push r0                         # pointer is in fp + 0x0
        push r1                         # position is in fp + 0x4
        push r2                         # value is in fp + 0x8

        # check if the pointer is valid
        loadi r1 _STD_ARRAY_STRUCT
        call std_address
        bz r0 _std_arrayfind_efault

        # check if the position is valid
        loadri r0 fp 0x0
        loadri r0 r0 _STD_ARRAY_SIZE
        loadri r1 fp 0x4
        bgeu r1 r0 _std_arrayfind_einval

        # search the element
        loadri r0 fp 0x0
        addi r0 r0 _STD_ARRAY_MEM
        loadri r1 fp 0x0
        loadri r1 r1 _STD_ARRAY_SIZE
        loadri r2 fp 0x4
        sub r1 r1 r2
        multi r1 r1 STD_WORDSIZE
        loadri r2 fp 0x8
        call std_find
        bz r1 _std_arrayfind_ret
        ret

        .label _std_arrayfind_ret
        divi r0 r0 STD_WORDSIZE
        loadri r1 fp 0x4
        add r0 r0 r1
        loadi r1 STD_NOERROR
        ret

        .label _std_arrayfind_einval
        loadi r1 STD_EINVAL
        ret

        .label _std_arrayfind_efault
        loadi r1 STD_EFAULT
        ret

# Get the times a value is found in the array.
# IN:  r0 pointer to the array
# IN:  r1 value
# OUT: r0 the times the value was found
# OUT: r1 error status
#      STD_NOERROR: no errors
#      STD_EFAULT: the pointer is not valid
.label std_arraycount
        push r0                         # pointer is in fp + 0x0
        push r1                         # value is in fp + 0x4

        # check if the pointer is valid
        loadi r1 _STD_ARRAY_STRUCT
        call std_address
        bz r0 _std_arraycount_efault

        # count the elements
        loadri r0 fp 0x0
        addi r0 r0 _STD_ARRAY_MEM
        loadri r1 fp 0x0
        loadri r1 r1 _STD_ARRAY_SIZE
        multi r1 r1 STD_WORDSIZE
        loadri r2 fp 0x4
        call std_count
        bz r1 _std_arraycount_ret
        ret

        .label _std_arraycount_ret
        loadi r1 STD_NOERROR
        ret

        .label _std_arraycount_efault
        loadi r1 STD_EFAULT
        ret
