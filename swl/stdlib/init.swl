# @file stdlib/init.swl
# Initialize the standard library.
#
#  Copyright (C) 2008-2011  Xos√© Otero <xoseotero@gmail.com>
#
#  This program is free software: you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published by
#  the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#
#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU General Public License
#  along with this program.  If not, see <http://www.gnu.org/licenses/>.


.include "stdlib/def.swl"
.include "stdlib/error.swl"
.include "stdlib/world.swl"

.include "stdlib/alloc/_init.swl"
.include "stdlib/int.swl"
.include "stdlib/address.swl"
.include "stdlib/time/_init.swl"
.include "stdlib/random/_seed.swl"
.include "stdlib/random/seed.swl"


# Flag to check if the standard library is initialized
.label _std_initialized
STD_FALSE


# Initialize the standard library.
# It's not a error to call this function several times, but only the first
# successfull call will initialize the standard library.
# IN:  g0 heap start
# IN:  g1 heap size
# OUT: g1 error status 
#      STD_ENOMEM: the memory if not enough
#      STD_EFAULT: the pointer is not valid
.label std_init
        # check if the standard library is initialized
        load g3 _std_initialized
        bnz g3 _std_init_ok

        # initialize the memory allocator
        call _std_minit
        bnz g1 _std_init_error

        # initialize the interrupts manager
        call _std_intinit
        bnz g1 _std_init_error

        # initialize the address check
        call _std_addressinit
        bnz g1 _std_init_error

        # initialize the time
        loadi g0 0x0
        call _std_timeinit
        bnz g1 _std_init_error

        # initialize the pseudorandom number generator
        world STD_MYPOSITION
        add g0 g1 g2
        load g1 _std_random_seed1
        add g0 g0 g1
        call std_seed                   # this call can't fail


        # set the state of the standard library to initialized
        loadi g0 STD_TRUE
        store g0 _std_initialized

        # exit without errors
        .label _std_init_ok
        loadi g1 STD_NOERROR
        ret

        # the call to std_minit failled
        .label _std_init_error
        # pass as the error the internal error
        ret
