# @file stdlib/map/remove.swl
# Remove a element from the map.
#
#  Copyright (C) 2012  Xos√© Otero <xoseotero@gmail.com>
#
#  This program is free software: you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published by
#  the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#
#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU General Public License
#  along with this program.  If not, see <http://www.gnu.org/licenses/>.


.include "stdlib/error.swl"
.include "stdlib/address/address.swl"
.include "stdlib/node/def.swl"
.include "stdlib/node/remove.swl"
.include "stdlib/map/def.swl"
.include "stdlib/map/_find.swl"


# Remove a element from the map.
# IN:  g0 pointer to the map
# IN:  g1 key
# OUT: g1 error status
#      STD_NOERROR: no errors
#      STD_EINVAL: the key is not in the map
#      STD_EFAULT: the pointer is not valid
.label std_mapremove
        move r0 g0                      # pointer
        move r1 g1                      # key

        # check if the pointer is valid
        loadi g1 STD_MAP_STRUCT
        call std_address
        bz g0 _std_mapremove_efault

        # search the key in the map
        move g0 r0
        move g1 r1
        call _std_mapfind
        bz g0 _std_mapremove_einval

        # remove the value
        loadri g1 r0 _STD_MAP_TOP
        bne g0 g1 _std_mapremove_rm
        # update top
        loadri g1 g0 _STD_NODE_NEXT
        storeri r0 g1 _STD_MAP_TOP

        .label _std_mapremove_rm
        call std_noderemove
        ret

        .label _std_mapremove_einval
        loadi g1 STD_EINVAL
        ret

        .label _std_mapremove_efault
        loadi g1 STD_EFAULT
        ret
