# @file stdlib/map/get.swl
# Get a value of the map.
#
#  Copyright (C) 2012  Xos√© Otero <xoseotero@gmail.com>
#
#  This program is free software: you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published by
#  the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#
#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU General Public License
#  along with this program.  If not, see <http://www.gnu.org/licenses/>.


.include "stdlib/error.swl"
.include "stdlib/address/address.swl"
.include "stdlib/map/def.swl"
.include "stdlib/map/_find.swl"


# Get a value of the map.
# IN:  g0 pointer to the map
# IN:  g1 key
# OUT: g0 element
# OUT: g1 error status
#      STD_NOERROR: no errors
#      STD_EINVAL: the key is not found
#      STD_EFAULT: the pointer is not valid
.label std_mapget
        move r0 g0                      # pointer
        move r1 g1                      # key

        # check if the pointer is valid
        loadi g1 STD_MAP_STRUCT
        call std_address
        bz g0 _std_mapget_efault

        # get the node
        move g0 r0
        move g1 r1
        call _std_mapfind
        bz g0 _std_mapget_einval

        .label _std_mapget_ret
        loadi g1 STD_NODE_DATA
        addi g1 g1 _STD_MAPNODE_VALUE
        loadrr g0 g0 g1
        loadi g1 STD_NOERROR
        ret

        .label _std_mapget_einval
        loadi g1 STD_EINVAL
        ret

        .label _std_mapget_efault
        loadi g1 STD_EFAULT
        ret
