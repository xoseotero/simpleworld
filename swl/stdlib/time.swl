# @file stdlib/time.swl
# Management of the Timer Interrupt to calculate the cycles of the World.
# std_init must be executed before using any of this functions.
#
#  Copyright (C) 2006-2010  Xos√© Otero <xoseotero@gmail.com>
#
#  This program is free software: you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published by
#  the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#
#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU General Public License
#  along with this program.  If not, see <http://www.gnu.org/licenses/>.

.include "stdlib/def.swl"
.include "stdlib/error.swl"
.include "stdlib/world.swl"
.include "stdlib/int.swl"

# current time
.label _std_time
.block STD_WORDSIZE


# Handler for the timer interrupt.
# IN:  r0 type of interrupt
.label _std_timerhandler
        load r0 _std_time
        addi r0 r0 0x1
        store r0 _std_time
        ret

# Initialize the time.
# IN:  r0 cycles
# OUT: r1 error status
#      STD_NOERROR: no errors
#      STD_ENOMEM:  there is not enough memory
.label _std_timeinit
        # initialize the time
        store r0 _std_time

        # insert the timer interrupt
        loadi r0 STD_ITIMER
        loada r1 _std_timerhandler
        call std_handler
        bnz r1 __std_timeinit_enomem

        .label __std_timeinit_ok
        loadi r1 STD_NOERROR
        ret

        .label __std_timeinit_enomem
        #loadi r1 STD_ENOMEM  # not needed because std_handler already sets it
        ret

# Get the current time.
# OUT: r0 cycles
.label std_time
        load r0 _std_time
        ret

# Sleep during some cycles.
# IN:  r0 cycles to sleep
.label std_sleep
        # don't sleep if cycles are 0
        bz r0 _std_sleep_ret

        load r1 _std_time
        add r1 r0 r1

        # loop while time < r0
        .label _std_sleep_loop
        world STD_NOTHING

        # r1 shouldn't be changed by world STD_NOTHING
        load r0 _std_time
        bltu r0 r1 _std_sleep_loop

        .label _std_sleep_ret
        ret
