# @file stdlib/stack/push.swl
# Insert a element in the stack.
#
#  Copyright (C) 2008-2012  Xos√© Otero <xoseotero@gmail.com>
#
#  This program is free software: you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published by
#  the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#
#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU General Public License
#  along with this program.  If not, see <http://www.gnu.org/licenses/>.


.include "stdlib/def.swl"
.include "stdlib/error.swl"
.include "stdlib/address/address.swl"
.include "stdlib/node/node.swl"
.include "stdlib/node/before.swl"
.include "stdlib/stack/def.swl"


# Insert a element in the stack.
# IN:  g0 pointer to the stack
# IN:  g1 value
# OUT: g1 error status
#      STD_NOERROR: no errors
#      STD_EFAULT: the pointer is not valid
#      STD_ENOMEM: there is not memory enough
.label std_stackpush
        move r0 g0                      # pointer
        move r1 g1                      # value

        # check if the pointer is valid
        loadi g1 STD_STACK_STRUCT
        call std_address
        bz g0 _std_stackpush_efault


        loadri g0 r0 _STD_STACK_TOP
        bz g0 _std_stackpush_empty
        .label _std_stackpush_notempty
        move g1 r1
        call std_nodebefore
        bnz g1 _std_stackpush_enomem
        b _std_stackpush_ret

        .label _std_stackpush_empty
        loadi g0 STD_NULL
        loadi g1 STD_NULL
        move g2 r1
        call std_node
        bnz g1 _std_stackpush_enomem

        .label _std_stackpush_ret
        # update the top pointer
        storeri r0 g0 _STD_STACK_TOP

        loadi g1 STD_NOERROR
        ret

        .label _std_stackpush_efault
        loadi g1 STD_EFAULT
        ret

        .label _std_stackpush_enomem
        loadi g1 STD_ENOMEM
        ret
