# @file stdlib/math.swl
# Math functions.
#
#  Copyright (C) 2006-2010  Xos√© Otero <xoseotero@gmail.com>
#
#  This program is free software: you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published by
#  the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#
#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU General Public License
#  along with this program.  If not, see <http://www.gnu.org/licenses/>.

.include "stdlib/def.swl"
.include "stdlib/error.swl"
.include "stdlib/address.swl"

# MAX and MIN for unsigned words
.label _std_math_word_zero              # A word with all the bits to 0
STD_ZERO
.label _std_math_word_full              # A word with all the bits to 1
STD_FULL


# Calculate the negative value.
# 0x80000000 can't be converted to positive and the same value is returned
# IN:  g0 the value
# OUT: g0 the negative value
.label std_neg
        load g1 _std_math_word_full
        xor g0 g0 g1
        addi g0 g0 0x0001

        ret

# Calculate the absolute value.
# 0x80000000 can't be converted to positive and the same value is returned
# IN:  g0 the value
# OUT: g0 the absolute value
.label std_abs
        # Check if the value is negative
        srli g1 g0 0x1F
        bz g1 _std_abs_ret
        call std_neg

        .label _std_abs_ret
        ret


# Calculate the pow.
# IN:  g0 base
# IN:  g1 exponent
# OUT: g0 the result
# OUT: g1 error status
#      STD_EOVERFLOW: value too large (not implemented)
.label std_pow
        move g2 g0
        load g3 _std_math_word_zero
        loadi g0 0x1

        .label _std_math_pow_continue
        beq g1 g3 _std_math_pow_return
        mult g0 g0 g2
        subi g1 g1 0x1
        b _std_math_pow_continue

        .label _std_math_pow_return
        ret


# Minimum signed value.
# IN:  g0 first value
# IN:  g1 second value
# OUT: g0 minimum value
.label std_min
        blt g1 g0 _std_min_second

        .label _std_min_first
        ret

        .label _std_min_second
        move g0 g1
        ret


# Minimum signed value of a block of memory.
# IN:  g0 pointer to the block of memory
# IN:  g1 size of the block of memory (must be a multiple of STD_WORDSIZE)
# OUT: g0 minimum value
# OUT: g1 error status
#      STD_NOERROR: no errors
#      STD_EINVAL: size is not valid
#      STD_EFAULT: the pointer is not valid
.label std_bmin
        push g0                         # pointer is in fp + 0x0
        push g1                         # size is in fp + 0x4

        # check if the size is valid
        bz g1 _std_bmin_einval
        modi g2 g1 STD_WORDSIZE
        bnz g2 _std_bmin_einval

        # check if the block is valid
        call std_address
        bz g0 _std_bmin_efault

        # restore the parameters
        loadri g0 fp 0x0
        loadri g1 fp 0x4

        # search the minimum value
        loadri g2 g0 0x0                # load the first element as the minimum

        .label _std_bmin_loop
        subi g1 g1 STD_WORDSIZE
        loadrr g3 g0 g1
        ble g2 g3 _std_bmin_continue
        move g2 g3                      # found a new minimum

        .label _std_bmin_continue
        bnz g1 _std_bmin_loop

        .label _std_bmin_ok
        move g0 g2
        loadi g1 STD_NOERROR
        ret

        .label _std_bmin_einval
        loadi g1 STD_EINVAL
        ret

        .label _std_bmin_efault
        loadi g1 STD_EFAULT
        ret

# Minimum signed value of a block of memory (half words).
# IN:  g0 pointer to the block of memory
# IN:  g1 size of the block of memory (must be a multiple of STD_WORDSIZE)
# OUT: g0 minimum value
# OUT: g1 error status
#      STD_NOERROR: no errors
#      STD_EINVAL: size is not valid
#      STD_EFAULT: the pointer is not valid
.label std_bminh
        push g0                         # pointer is in fp + 0x0
        push g1                         # size is in fp + 0x4

        # check if the size is valid
        bz g1 _std_bminh_einval
        modi g2 g1 STD_HWORDSIZE
        bnz g2 _std_bminh_einval

        # check if the block is valid
        call std_address
        bz g0 _std_bminh_efault

        # restore the parameters
        loadri g0 fp 0x0
        loadri g1 fp 0x4

        # search the minimum value
        loadhri g2 g0 0x0               # load the first element as the minimum
        signh g2 g2

        .label _std_bminh_loop
        subi g1 g1 STD_HWORDSIZE
        loadhrr g3 g0 g1
        signh g3 g3
        ble g2 g3 _std_bminh_continue
        move g2 g3                      # found a new minimum

        .label _std_bminh_continue
        bnz g1 _std_bminh_loop

        .label _std_bminh_ok
        move g0 g2
        loadi g1 STD_NOERROR
        ret

        .label _std_bminh_einval
        loadi g1 STD_EINVAL
        ret

        .label _std_bminh_efault
        loadi g1 STD_EFAULT
        ret

# Minimum signed value of a block of memory (quarter words).
# IN:  g0 pointer to the block of memory
# IN:  g1 size of the block of memory
# OUT: g0 minimum value
# OUT: g1 error status
#      STD_NOERROR: no errors
#      STD_EINVAL: size is not valid
#      STD_EFAULT: the pointer is not valid
.label std_bminq
        push g0                         # pointer is in fp + 0x0
        push g1                         # size is in fp + 0x4

        # check if the size is valid
        bz g1 _std_bminh_einval

        # check if the block is valid
        call std_address
        bz g0 _std_bminq_efault

        # restore the parameters
        loadri g0 fp 0x0
        loadri g1 fp 0x4

        # search the minimum value
        loadqri g2 g0 0x0               # load the first element as the minimum
        signq g2 g2

        .label _std_bminq_loop
        subi g1 g1 STD_QWORDSIZE
        loadqrr g3 g0 g1
        signq g3 g3
        ble g2 g3 _std_bminq_continue
        move g2 g3                      # found a new minimum

        .label _std_bminq_continue
        bnz g1 _std_bminq_loop

        .label _std_bminq_ok
        move g0 g2
        loadi g1 STD_NOERROR
        ret

        .label _std_bminq_einval
        loadi g1 STD_EINVAL
        ret

        .label _std_bminq_efault
        loadi g1 STD_EFAULT
        ret


# Minimum unsigned value.
# IN:  g0 first value
# IN:  g1 second value
# OUT: g0 minimum value
.label std_minu
        bltu g1 g0 _std_minu_second

        .label _std_minu_first
        ret

        .label _std_minu_second
        move g0 g1
        ret


# Minimum unsigned value of a block of memory.
# IN:  g0 pointer to the block of memory
# IN:  g1 size in words of the block of memory
# OUT: g0 minimum value
# OUT: g1 error status
#      STD_NOERROR: no errors
#      STD_EFAULT: the pointer is not valid
.label std_bminu
        push g0                         # pointer is in fp + 0x0
        push g1                         # size is in fp + 0x4

        # check if the block is valid
        call std_address
        bz g0 _std_bminu_efault

        # restore the parameters
        loadri g0 fp 0x0
        loadri g1 fp 0x4

        # search the minimum unsigned value
        loadri g2 g0 0x0                # load the first element as the minimum

        .label _std_bminu_loop
        subi g1 g1 STD_WORDSIZE
        loadrr g3 g0 g1
        bleu g2 g3 _std_bminu_continue
        move g2 g3                      # found a new minimum

        .label _std_bminu_continue
        bnz g1 _std_bminu_loop

        .label _std_bminu_ok
        move g0 g2
        loadi g1 STD_NOERROR
        ret

        .label _std_bminu_efault
        loadi g1 STD_EFAULT
        ret

# Minimum unsigned value of a block of memory (half words).
# IN:  g0 pointer to the block of memory
# IN:  g1 size in half words of the block of memory
# OUT: g0 minimum value
# OUT: g1 error status
#      STD_NOERROR: no errors
#      STD_EFAULT: the pointer is not valid
.label std_bminuh
        push g0                         # pointer is in fp + 0x0
        push g1                         # size is in fp + 0x4

        # check if the block is valid
        call std_address
        bz g0 _std_bminuh_efault

        # restore the parameters
        loadri g0 fp 0x0
        loadri g1 fp 0x4

        # search the minimum unsigned value
        loadhri g2 g0 0x0               # load the first element as the minimum

        .label _std_bminuh_loop
        subi g1 g1 STD_HWORDSIZE
        loadhrr g3 g0 g1
        bleu g2 g3 _std_bminuh_continue
        move g2 g3                      # found a new minimum

        .label _std_bminuh_continue
        bnz g1 _std_bminuh_loop

        .label _std_bminuh_ok
        move g0 g2
        loadi g1 STD_NOERROR
        ret

        .label _std_bminuh_efault
        loadi g1 STD_EFAULT
        ret

# Minimum unsigned value of a block of memory (quarter words).
# IN:  g0 pointer to the block of memory
# IN:  g1 size in quarter words of the block of memory
# OUT: g0 minimum value
# OUT: g1 error status
#      STD_NOERROR: no errors
#      STD_EFAULT: the pointer is not valid
.label std_bminuq
        push g0                         # pointer is in fp + 0x0
        push g1                         # size is in fp + 0x4

        # check if the block is valid
        call std_address
        bz g0 _std_bminuq_efault

        # restore the parameters
        loadri g0 fp 0x0
        loadri g1 fp 0x4

        # search the minimum unsigned value
        loadqri g2 g0 0x0               # load the first element as the minimum

        .label _std_bminuq_loop
        subi g1 g1 STD_QWORDSIZE
        loadqrr g3 g0 g1
        bleu g2 g3 _std_bminuq_continue
        move g2 g3                      # found a new minimum

        .label _std_bminuq_continue
        bnz g1 _std_bminuq_loop

        .label _std_bminuq_ok
        move g0 g2
        loadi g1 STD_NOERROR
        ret

        .label _std_bminuq_efault
        loadi g1 STD_EFAULT
        ret


# Maximum signed value of a block of memory.
# IN:  g0 first value
# IN:  g1 second value
# OUT: g0 maximum value
.label std_max
        bgt g1 g0 _std_max_second

        .label _std_max_first
        ret

        .label _std_max_second
        move g0 g1
        ret


# Maximum signed value of a block of memory.
# IN:  g0 pointer to the block of memory
# IN:  g1 size in words of the block of memory
# OUT: g0 maximum value
# OUT: g1 error status
#      STD_NOERROR: no errors
#      STD_EFAULT: the pointer is not valid
.label std_bmax
        push g0                         # pointer is in fp + 0x0
        push g1                         # size is in fp + 0x4

        # check if the block is valid
        call std_address
        bz g0 _std_bmax_efault

        # restore the parameters
        loadri g0 fp 0x0
        loadri g1 fp 0x4

        # search the maximum value
        loadri g2 g0 0x0                # load the first element as the minimum

        .label _std_bmax_loop
        subi g1 g1 STD_WORDSIZE
        loadrr g3 g0 g1
        bge g2 g3 _std_bmax_continue
        move g2 g3                      # found a new minimum

        .label _std_bmax_continue
        bnz g1 _std_bmax_loop

        .label _std_bmax_ok
        move g0 g2
        loadi g1 STD_NOERROR
        ret

        .label _std_bmax_efault
        loadi g1 STD_EFAULT
        ret

# Maximum signed value of a block of memory (half words).
# IN:  g0 pointer to the block of memory
# IN:  g1 size in half words of the block of memory
# OUT: g0 maximum value
# OUT: g1 error status
#      STD_NOERROR: no errors
#      STD_EFAULT: the pointer is not valid
.label std_bmaxh
        push g0                         # pointer is in fp + 0x0
        push g1                         # size is in fp + 0x4

        # check if the block is valid
        call std_address
        bz g0 _std_bmaxh_efault

        # restore the parameters
        loadri g0 fp 0x0
        loadri g1 fp 0x4

        # search the maximum value
        loadhri g2 g0 0x0               # load the first element as the minimum
        signh g2 g2

        .label _std_bmaxh_loop
        subi g1 g1 STD_HWORDSIZE
        loadhrr g3 g0 g1
        signh g3 g3
        bge g2 g3 _std_bmaxh_continue
        move g2 g3                      # found a new minimum

        .label _std_bmaxh_continue
        bnz g1 _std_bmaxh_loop

        .label _std_bmaxh_ok
        move g0 g2
        loadi g1 STD_NOERROR
        ret

        .label _std_bmaxh_efault
        loadi g1 STD_EFAULT
        ret

# Maximum signed value of a block of memory (quarter words).
# IN:  g0 pointer to the block of memory
# IN:  g1 size in quarter words of the block of memory
# OUT: g0 maximum value
# OUT: g1 error status
#      STD_NOERROR: no errors
#      STD_EFAULT: the pointer is not valid
.label std_bmaxq
        push g0                         # pointer is in fp + 0x0
        push g1                         # size is in fp + 0x4

        # check if the block is valid
        call std_address
        bz g0 _std_bmaxq_efault

        # restore the parameters
        loadri g0 fp 0x0
        loadri g1 fp 0x4

        # search the maximum value
        loadqri g2 g0 0x0               # load the first element as the minimum
        signq g2 g2

        .label _std_bmaxq_loop
        subi g1 g1 STD_QWORDSIZE
        loadqrr g3 g0 g1
        signq g3 g3
        bge g2 g3 _std_bmaxq_continue
        move g2 g3                      # found a new minimum

        .label _std_bmaxq_continue
        bnz g1 _std_bmaxq_loop

        .label _std_bmaxq_ok
        move g0 g2
        loadi g1 STD_NOERROR
        ret

        .label _std_bmaxq_efault
        loadi g1 STD_EFAULT
        ret


# Maximum signed value of a block of memory.
# IN:  g0 first value
# IN:  g1 second value
# OUT: g0 maximum value
.label std_maxu
        bgtu g1 g0 _std_maxu_second

        .label _std_maxu_first
        ret

        .label _std_maxu_second
        move g0 g1
        ret


# Maximum unsigned value of a block of memory.
# IN:  g0 pointer to the block of memory
# IN:  g1 size in words of the block of memory
# OUT: g0 maximum value
# OUT: g1 error status
#      STD_NOERROR: no errors
#      STD_EFAULT: the pointer is not valid
.label std_bmaxu
        push g0                         # pointer is in fp + 0x0
        push g1                         # size is in fp + 0x4

        # check if the block is valid
        call std_address
        bz g0 _std_bmaxu_efault

        # restore the parameters
        loadri g0 fp 0x0
        loadri g1 fp 0x4

        # search the maximum (unsigned) value
        loadri g2 g0 0x0                # load the first element as the minimum

        .label _std_bmaxu_loop
        subi g1 g1 STD_WORDSIZE
        loadrr g3 g0 g1
        bgeu g2 g3 _std_bmaxu_continue
        move g2 g3                      # found a new minimum

        .label _std_bmaxu_continue
        bnz g1 _std_bmaxu_loop

        .label _std_bmaxu_ok
        move g0 g2
        loadi g1 STD_NOERROR
        ret

        .label _std_bmaxu_efault
        loadi g1 STD_EFAULT
        ret

# Maximum unsigned value of a block of memory (half words).
# IN:  g0 pointer to the block of memory
# IN:  g1 size in half words of the block of memory
# OUT: g0 maximum value
# OUT: g1 error status
#      STD_NOERROR: no errors
#      STD_EFAULT: the pointer is not valid
.label std_bmaxuh
        push g0                         # pointer is in fp + 0x0
        push g1                         # size is in fp + 0x4

        # check if the block is valid
        call std_address
        bz g0 _std_bmaxuh_efault

        # restore the parameters
        loadri g0 fp 0x0
        loadri g1 fp 0x4

        # search the maximum (unsigned) value
        loadhri g2 g0 0x0               # load the first element as the minimum
        signh g2 g2

        .label _std_bmaxuh_loop
        subi g1 g1 STD_HWORDSIZE
        loadhrr g3 g0 g1
        bgeu g2 g3 _std_bmaxuh_continue
        move g2 g3                      # found a new minimum

        .label _std_bmaxuh_continue
        bnz g1 _std_bmaxuh_loop

        .label _std_bmaxuh_ok
        move g0 g2
        loadi g1 STD_NOERROR
        ret

        .label _std_bmaxuh_efault
        loadi g1 STD_EFAULT
        ret

# Maximum unsigned value of a block of memory (quarter words).
# IN:  g0 pointer to the block of memory
# IN:  g1 size in quarter words of the block of memory
# OUT: g0 maximum value
# OUT: g1 error status
#      STD_NOERROR: no errors
#      STD_EFAULT: the pointer is not valid
.label std_bmaxuq
        push g0                         # pointer is in fp + 0x0
        push g1                         # size is in fp + 0x4

        # check if the block is valid
        call std_address
        bz g0 _std_bmaxuq_efault

        # restore the parameters
        loadri g0 fp 0x0
        loadri g1 fp 0x4

        # search the maximum (unsigned) value
        loadqri g2 g0 0x0               # load the first element as the minimum
        signq g2 g2

        .label _std_bmaxuq_loop
        subi g1 g1 STD_QWORDSIZE
        loadqrr g3 g0 g1
        bgeu g2 g3 _std_bmaxuq_continue
        move g2 g3                      # found a new minimum

        .label _std_bmaxuq_continue
        bnz g1 _std_bmaxuq_loop

        .label _std_bmaxuq_ok
        move g0 g2
        loadi g1 STD_NOERROR
        ret

        .label _std_bmaxuq_efault
        loadi g1 STD_EFAULT
        ret


# Summation of a block of memory.
# IN:  g0 pointer to the block of memory
# IN:  g1 size in words of the block of memory
# OUT: g0 summation value
# OUT: g1 error status
#      STD_NOERROR: no errors
#      STD_EFAULT: the pointer is not valid
.label std_bsum
        push g0                         # pointer is in fp + 0x0
        push g1                         # size is in fp + 0x4

        # check if the block is valid
        call std_address
        bz g0 _std_bsum_efault

        # restore the parameters
        loadri g0 fp 0x0
        loadri g1 fp 0x4

        # calculate the summation
        loadi g2 0x0                    # begin with 0

        .label _std_bsum_loop
        subi g1 g1 STD_WORDSIZE
        loadrr g3 g0 g1
        add g2 g2 g3
        bnz g1 _std_bsum_loop

        .label _std_bsum_ok
        move g0 g2
        loadi g1 STD_NOERROR
        ret

        .label _std_bsum_efault
        loadi g1 STD_EFAULT
        ret

# Summation of a block of memory (half words).
# IN:  g0 pointer to the block of memory
# IN:  g1 size in half words of the block of memory
# OUT: g0 summation value
# OUT: g1 error status
#      STD_NOERROR: no errors
#      STD_EFAULT: the pointer is not valid
#      STD_EOVERFLOW: value too large (not implemented)
.label std_bsumh
        push g0                         # pointer is in fp + 0x0
        push g1                         # size is in fp + 0x4

        # check if the block is valid
        call std_address
        bz g0 _std_bsumh_efault

        # restore the parameters
        loadri g0 fp 0x0
        loadri g1 fp 0x4

        # calculate the summation
        loadi g2 0x0                    # begin with 0

        .label _std_bsumh_loop
        subi g1 g1 STD_HWORDSIZE
        loadhrr g3 g0 g1
        add g2 g2 g3
        bnz g1 _std_bsumh_loop

        .label _std_bsumh_ok
        move g0 g2
        loadi g1 STD_NOERROR
        ret

        .label _std_bsumh_efault
        loadi g1 STD_EFAULT
        ret

# Summation of a block of memory (quarter words).
# IN:  g0 pointer to the block of memory
# IN:  g1 size in quarter words of the block of memory
# OUT: g0 summation value
# OUT: g1 error status
#      STD_NOERROR: no errors
#      STD_EFAULT: the pointer is not valid
#      STD_EOVERFLOW: value too large (not implemented)
.label std_bsumq
        push g0                         # pointer is in fp + 0x0
        push g1                         # size is in fp + 0x4

        # check if the block is valid
        call std_address
        bz g0 _std_bsumq_efault

        # restore the parameters
        loadri g0 fp 0x0
        loadri g1 fp 0x4

        # calculate the summation
        loadi g2 0x0                    # begin with 0

        .label _std_bsumq_loop
        subi g1 g1 STD_QWORDSIZE
        loadqrr g3 g0 g1
        add g2 g2 g3
        bnz g1 _std_bsumq_loop

        .label _std_bsumq_ok
        move g0 g2
        loadi g1 STD_NOERROR
        ret

        .label _std_bsumq_efault
        loadi g1 STD_EFAULT
        ret


# Average (signed) value of a block of memory.
# IN:  g0 pointer to the block of memory
# IN:  g1 size in words of the block of memory
# OUT: g0 average value
# OUT: g1 error status
#      STD_NOERROR: no errors
#      STD_EFAULT: the pointer is not valid
.label std_bavg
        push g1                         # size is in fp + 0x0

        # calculate the summation
        call std_bsum
        bnz g1 _std_bavg_efault

        # restore the parameters
        loadri g1 fp 0x0

        # calculate the average value
        divi g1 g1 STD_WORDSIZE
        div g0 g0 g1

        .label _std_bavg_ok
        loadi g1 STD_NOERROR
        ret

        .label _std_bavg_efault
        #loadi g1 STD_EFAULT            # not needed because std_bsum already
                                        # sets it
        ret

# Average (signed) value of a block of memory (half words).
# IN:  g0 pointer to the block of memory
# IN:  g1 size in half words of the block of memory
# OUT: g0 average value
# OUT: g1 error status
#      STD_NOERROR: no errors
#      STD_EFAULT: the pointer is not valid
.label std_bavgh
        push g1                         # size is in fp + 0x0

        # calculate the summation
        call std_bsumh
        bnz g1 _std_bavgh_efault

        # restore the parameters
        loadri g1 fp 0x0

        # calculate the average value
        divi g1 g1 STD_HWORDSIZE 
        div g0 g0 g1

        .label _std_bavgh_ok
        loadi g1 STD_NOERROR
        ret

        .label _std_bavgh_efault
        #loadi g1 STD_EFAULT            # not needed because std_bsumh already
                                        # sets it
        ret

# Average (signed) value of a block of memory (quarter words).
# IN:  g0 pointer to the block of memory
# IN:  g1 size in quarter words of the block of memory
# OUT: g0 average value
# OUT: g1 error status
#      STD_NOERROR: no errors
#      STD_EFAULT: the pointer is not valid
.label std_bavgq
        push g1                         # size is in fp + 0x0

        # calculate the summation
        call std_bsumq
        bnz g1 _std_bavgq_efault

        # restore the parameters
        loadri g1 fp 0x0

        # calculate the average value
        #divi g1 g1 STD_QWORDSIZE
        div g0 g0 g1

        .label _std_bavgq_ok
        loadi g1 STD_NOERROR
        ret

        .label _std_bavgq_efault
        #loadi g1 STD_EFAULT            # not needed because std_bsumq already
                                        # sets it
        ret
