# @file stdlib/list/remove.swl
# Remove a element from the list.
#
#  Copyright (C) 2008-2012  Xos√© Otero <xoseotero@gmail.com>
#
#  This program is free software: you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published by
#  the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#
#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU General Public License
#  along with this program.  If not, see <http://www.gnu.org/licenses/>.


.include "stdlib/error.swl"
.include "stdlib/def.swl"
.include "stdlib/address/address.swl"
.include "stdlib/node/remove.swl"
.include "stdlib/node/next.swl"
.include "stdlib/list/def.swl"


# Remove a element from the list.
# IN:  g0 pointer to the list
# IN:  g1 position
# OUT: g1 error status
#      STD_NOERROR: no errors
#      STD_EINVAL: the position is out of range
#      STD_EFAULT: the pointer is not valid
.label std_listremove
        move r0 g0                      # pointer
        move r1 g1                      # position

        # check if the pointer is valid
        loadi g1 STD_LIST_STRUCT
        call std_address
        bz g0 _std_listremove_efault

        # check if the position is valid
        loadri r2 r0 _STD_LIST_SIZE     # size
        bgeu r1 r2 _std_listremove_einval

        # check if the list will be empty
        loadi g0 0x1
        beq r2 g0 _std_listremove_empty
        # check if the remove is at the begining of the list
        bz r1 _std_listremove_beginning

        .label _std_listremove_notbeginning
        loadri g0 r0 _STD_LIST_FIRST
        move g1 r1
        call std_next
        bnz g1 _std_listremove_efault
        call std_noderemove
        b _std_listremove_ret

        .label _std_listremove_empty
        loadri g0 r0 _STD_LIST_FIRST
        call std_noderemove
        loadi g0 STD_NULL
        storeri r0 g0 _STD_LIST_FIRST
        b _std_listremove_ret

        .label _std_listremove_beginning
        loadri g0 r0 _STD_LIST_FIRST
        loadi g1 0x1
        call std_next
        bnz g1 _std_listremove_efault
        move r3 g0                      # new first node
        loadri g0 r0 _STD_LIST_FIRST
        call std_nodebefore
        storeri r0 r3 _STD_LIST_FIRST

        .label _std_listremove_ret
        # update the size
        subi r2 r2 0x1
        storeri r0 r2 _STD_LIST_SIZE

        loadi g1 STD_NOERROR
        ret

        .label _std_listremove_einval
        loadi g1 STD_EINVAL
        ret

        .label _std_listremove_efault
        loadi g1 STD_EFAULT
        ret
