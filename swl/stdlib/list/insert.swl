# @file stdlib/list/insert.swl
# Insert a element in the list.
#
#  Copyright (C) 2008-2012  Xos√© Otero <xoseotero@gmail.com>
#
#  This program is free software: you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published by
#  the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#
#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU General Public License
#  along with this program.  If not, see <http://www.gnu.org/licenses/>.


.include "stdlib/error.swl"
.include "stdlib/address/address.swl"
.include "stdlib/node/node.swl"
.include "stdlib/node/before.swl"
.include "stdlib/node/after.swl"
.include "stdlib/node/next.swl"
.include "stdlib/list/def.swl"


# Insert a element in the list.
# IN:  g0 pointer to the list
# IN:  g1 position
# IN:  g2 value
# OUT: g1 error status
#      STD_NOERROR: no errors
#      STD_EINVAL: the position is out of range
#      STD_EFAULT: the pointer is not valid
#      STD_ENOMEM: there is not memory enough
.label std_listinsert
        move r0 g0                      # pointer
        move r1 g1                      # position
        move r2 g2                      # value

        # check if the pointer is valid
        loadi g1 STD_LIST_STRUCT
        call std_address
        bz g0 _std_listinsert_efault

        # check if the position is valid
        loadri r3 r0 _STD_LIST_SIZE     # size
        bgtu r1 r3 _std_listinsert_einval

        # check if the list is empty
        bz r3 _std_listinsert_empty
        # check if the insert is at the begining of the list
        bz r1 _std_listinsert_beginning

        .label _std_listinsert_notbeginning
        loadri g0 r0 _STD_LIST_FIRST
        subi g1 r1 0x1
        call std_next
        bnz g1 _std_listinsert_efault
        move g1 r2
        call std_nodeafter
        bnz g1 _std_listinsert_enomem
        b _std_listinsert_ret

        .label _std_listinsert_empty
        move g0 r2
        call std_node
        bnz g1 _std_listinsert_enomem
        storeri r0 g0 _STD_LIST_FIRST
        b _std_listinsert_ret

        .label _std_listinsert_beginning
        loadri g0 r0 _STD_LIST_FIRST
        move g1 r2
        call std_nodebefore
        bnz g1 _std_listinsert_enomem
        storeri r0 g0 _STD_LIST_FIRST

        .label _std_listinsert_ret
        # update the size
        addi r3 r3 0x1
        storeri r0 r3 _STD_LIST_SIZE

        loadi g1 STD_NOERROR
        ret

        .label _std_listinsert_einval
        loadi g1 STD_EINVAL
        ret

        .label _std_listinsert_efault
        loadi g1 STD_EFAULT
        ret

        .label _std_listinsert_enomem
        loadi g1 STD_ENOMEM
        ret
